#!/usr/bin/env perl

use strict;
use File::Spec::Functions qw( splitpath splitdir catpath catdir catfile rootdir );
use Cwd;

die "Usage: r <cmd_to_exec>\n\n  Find all lib's to add to PERL5LIB\n" unless @ARGV;

my $mark_file = '.pbs_project';
my ($vol, $cur_dir) = splitpath(getcwd(), 1);
$cur_dir = [splitdir($cur_dir)];

while (1) {
  my $dir = catpath($vol, catdir(@$cur_dir));

  if (-e catfile($dir, $mark_file) and -f _) {
    $ENV{PERL5LIB} = join(':', _find_libs($dir), split(/:/, $ENV{PERL5LIB} || ''));
    chdir($dir);
    exec(@ARGV);
    die "Failed to exec '@ARGV': $!";
  }

  last if $ENV{HOME} and $dir eq $ENV{HOME};
  last if $dir eq rootdir();

  pop @$cur_dir;
}

die "Failed to find '$mark_file' file\n";


### Find all lib deps

sub _find_libs {
  my @dirs = @_;

  my @found;
  while (my $dir = shift @dirs) {
    my $l = catdir($dir, 'lib');
    push @found, $l if -d $l;

    my $elibs = catdir($dir, 'elib');
    next unless opendir(my $dh, $elibs);
    push @dirs, grep { -d $_ } map { catfile($elibs, $_) } grep {m/^[^\.]/} readdir($dh);
  }

  return @found;
}
