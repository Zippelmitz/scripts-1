#!/usr/bin/env perl

use strict;
use warnings;
use Cwd;
use File::Spec::Functions qw( splitdir catdir catfile );
use JSON;
use Getopt::Long;
use Data::Dump ();
use DateTime;

my %cfg = (
  verbose => 0,
  quiet   => 0,
);
my $ok = GetOptions(\%cfg, "help", "verbose+", "quiet", "dryrun|n");
usage() unless $ok;
usage() if $cfg{help};

dispatch_command(\%cfg, @ARGV);


sub dispatch_command {
  my ($cfg, $cmd, @args) = @_;

  if (!defined($cmd)) {
    usage();
    return;
  }

  if ($cmd eq 'deploy') {
    load_cfg($cfg);
    deploy($cfg, @args);
  }
  elsif ($cmd eq 'environments') {
    load_cfg($cfg);
    environments($cfg);
  }
  elsif ($cmd eq 'setup_bash_complete') {
    setup_bash_completion($cfg);
  }
  else {
    usage("Command '$cmd' not understood");
  }

  return;
}


##########
# Commands

sub deploy {
  my ($cfg, $env) = @_;
  my $pcfg = $cfg->{project};
  my $pname = $pcfg->{project}{name} || (find_project_root())[-1];

  usage('Deploy requires an environment as parameter.',
    'Valid values are: ' . join(', ', sort keys %{$pcfg->{environments}}))
    unless defined $env;

  ### Decide which environment to use
  my $ecfg = $pcfg->{environments}{$env};
  usage("Environment '$env' not found in rules.prj") unless $ecfg;
  debug("Using environment '$env'");

  ### Prepare base rsync command to run
  my $rsync = $pcfg->{commands}{rsync};
  $rsync = [split(/\s+/, $rsync)] if $rsync && !ref($rsync);
  $rsync ||= ['rsync', '-a'];

  ### Include excludes
  if (my $excl = $pcfg->{paths}{exclude}) {
    $excl = [$excl] unless ref($excl) eq 'ARRAY';
    push @$rsync, map { ('--exclude', $_) } @$excl;
  }

  ### Add common rsync options
  push @$rsync, '-v' if $cfg->{verbose} || $pcfg->{verbose};
  push @$rsync, '-n' if $cfg->{dryrun};
  push @$rsync, "$cfg->{root}/";

  ### Hosts to use
  my @hosts = @{$ecfg->{hosts} || []};
  push @hosts, $ecfg if $ecfg->{host};
  fatal("Environment '$env' missing 'host' or 'hosts' entry: $ecfg")
    unless @hosts;

  ### Check for proper branch
  if (my $branch_name = $ecfg->{branch}) {
    my $cur_branch = _current_branch_name();
    fatal("Current branch '$cur_branch' not the expected one '$branch_name")
      unless $cur_branch eq $branch_name;
  }

  ### Check for tags
  if ($ecfg->{tag}) {
    _tag_release($env);
  }

  ### Describe the current release
  END { unlink('.release') }
  system('git describe --always --tags > .release');
  system('pwd >> .release');
  system('git show HEAD >> .release');

  ### rsync to each one
  foreach my $host (@hosts) {
    fatal("Env '$env' with missing 'host'") unless $host->{host};

    my @cmd = @$rsync;

    ### prepare the SSH transport settings
    push @cmd, '-e', 'ssh';
    $cmd[-1] .= " -l $host->{user}" if $host->{user};
    $cmd[-1] .= " -p $host->{port}" if $host->{port};

    push @cmd, join(':', $host->{host}, ($host->{path} || $ecfg->{path}));

    debug('Rsync command:', \@cmd);
    system(@cmd) unless $cfg{dryrun};

    if (exists $ecfg->{post_deploy}) {
      @cmd = ('ssh', $host->{host});
      push(@cmd, '-v') if $cfg->{verbose} || $pcfg->{verbose};
      push(@cmd, '-l', $host->{user}) if $host->{user};
      push(@cmd, '-p', $host->{port}) if $host->{port};
      push @cmd,
        "cd " . ($host->{path} || $ecfg->{path}) . " && $ecfg->{post_deploy}";

      debug('Post deploy command:', \@cmd);
      system(@cmd) unless $cfg{dryrun};
    }
  }

  notify($cfg, "Sucessfully deployed '$env' for project '$pname'", $pname, $env);
  return;
}

sub environments {
  my ($cfg) = @_;

  my $pretty = sub { print @_, "\n" };
  if (is_tty()) {
    print "List of available environments:\n";
    $pretty = sub { print ' * ', @_, "\n" };
  }

  my $pcfg = $cfg->{project};
  $pretty->($_) for sort keys %{$pcfg->{environments}};
}

sub setup_bash_completion {
  print <<'EOBC';

### bash-completion setup code for prj app
#
# Add
#
#    prj_comp=`mktemp -t prj_completion.XXXXXXX`
#    if [ -e "$prj_comp" ] ; then
#      prj setup_bash_complete >> "$prj_comp"
#      source "$prj_comp"
#      rm -f "$prj_comp"
#    fi
#    unset prj_comp
#
# to your .bashrc or .bash_profile

_prj_complete()
{
  local cur prev commands
  _get_comp_words_by_ref cur prev

  commands='deploy environments'

  COMPREPLY=()
  if [ $COMP_CWORD -eq 1 ] ; then
      COMPREPLY=( $( compgen -W "$commands" -- "$cur" ) )
  elif [ $COMP_CWORD -gt 1 -a "$prev" == "deploy" ] ; then
    envs=`prj environments`
    COMPREPLY=( $( compgen -W "$envs" -- "$cur" ) )
  fi
}

complete -F _prj_complete -o default prj

EOBC
}


#######
# Usage

sub usage {
  my ($msg, @others) = @_;

  print STDERR <<EOU;
Usage: prj COMMAND ARGS

The current list of COMMANDs is:

    deploy    deploys current version to an environment

EOU

  print STDERR "ERROR: $msg\n" if $msg;
  foreach my $err (@others) {
    print ' => ', $err, "\n";
  }
  exit(1);
}


########
# Logger

sub debug {
  return if $cfg{quiet};
  return unless $cfg{verbose} > 1;

  return _log('DEBUG', @_);
}

sub fatal {
  _log('FATAL', @_);
  exit(1);
}

sub _log {
  return unless is_tty();

  my $tag = shift;
  my $msg = join(' ', map { ref($_) ? Data::Dump::pp($_) : $_ } @_);

  foreach my $line (split(/\n\r?/, $msg)) {
    print STDERR "[$tag]: $line\n";
  }

  return;
}

#######
# Utils

sub load_cfg {
  my ($cfg) = @_;
  my $root = $cfg{root} ||= find_project_root();
  my $prj_cfg;

  if (open(my $fh, '<', catfile($root, 'rules.prj'))) {
    local $/;
    $prj_cfg = decode_json(<$fh>);
    close($fh);
  }
  else {
    fatal("Could not open 'rules.prj' file in $root: $!");
  }

  $cfg->{project} = $prj_cfg;
}

sub find_project_root {
  my $project_root;

  my @cwd = splitdir(getcwd());
  do {
    my $cwd = catdir(@cwd);

    if (-e catfile($cwd, 'rules.prj')) {
      $project_root = $cwd;
    }
    elsif ($cwd eq $ENV{HOME} || scalar(@cwd) == 1) {
      fatal("Could not find project root (was looking for rules.prj)");
    }
    else {
      pop @cwd;
    }
  } while (!$project_root);

  return @cwd if wantarray;
  return $project_root;
}

sub notify {
  my ($cfg, $message, $proj, $env) = @_;

  return if $cfg->{dryrun};
  my $pid = fork();
  fatal("Could not fork(), could not notify: $!") unless defined $pid;
  return if $pid;  ## Parent leaves

  ## Child tries to notify
  eval { require WebService::Notifo };
  if (!$@) {
    my $n = WebService::Notifo->new;
    $n->send_notification(
      label => 'prj',
      title => "$proj ($env)",
      msg   => $message
    );
  }
  elsif (my $gn = _has_cmd('growlnotify')) {
    system($gn, '-m', $message, 'prj deploy status');
  }

  return;
}

sub is_tty { return -t \*STDOUT }

sub _has_cmd {
  my ($cmd) = @_;

  for my $path (split(/:/, $ENV{PATH})) {
    my $fn = catfile($path, $cmd);
    return $fn if -x $fn;
  }

  return;
}


############
# Operations

sub _tag_release {
  my ($env) = @_;

  my $now = DateTime->now;
  my $tag = join('-', 'release', $env, $now->ymd('') . $now->hms(''));

  return system('git', 'tag', $tag);
}

sub _current_branch_name {
  my $name = `git symbolic-ref HEAD`;
  chomp $name;
  ($name) = $name =~ m{^refs/[^/]+/(.+)};
  return $name;
}
