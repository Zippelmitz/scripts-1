#!/usr/bin/env perl

use strict;
use warnings;
use Mac::FSEvents;
use IO::Select;
use Getopt::Long;
use File::Spec::Functions qw( catfile );
use File::HomeDir;

my $once;
my $clear;
my $script;
my $ok = GetOptions(
  'once'     => \$once,
  'clear'    => \$clear,
  'script=s' => \$script,
);
usage() unless $ok;

my @args = @ARGV;
if ($script) {
  my $path = catfile(File::HomeDir->my_home, '.test-continuous.d', $script);
  usage(1, "Script '~/.test-continuous.d/$script' not found or not executable")
    unless -e $path && -x _;
  unshift @args, $path;
}

usage(1, "A command_to_execute is required") unless @args;

run_command(@args) if $once;

my @paths = ('./lib', './t');
my %fh_map;
my $sel = IO::Select->new;

foreach my $path (@paths) {
  my $fs = Mac::FSEvents->new({
    path    => $path,
    latency => 1.0,
  });
  my $fh = $fs->watch;
  $sel->add($fh);
  $fh_map{$fh} = $fs;
}

while (my @fhs = $sel->can_read) {
  my $count = 0;
  foreach my $fh (@fhs) {
    $count += $fh_map{$fh}->read_events;
  }
  run_command(@args) if $count;
}

exit(0);

END { map { $_->stop } keys %fh_map }


### Run the command
sub run_command {
  system('/usr/bin/clear') if $clear;
  system(@_);
  print STDERR "Executed: ", join(' ', @_), "\n";
}


### Usage stuff
sub usage {
  my ($code, $mesg) = @_;
  
  print STDERR <<'...';
Usage: x-test-continuous [options] [command_to_run]

  This utility watches the ./lib and ./t for modifications.
  If any modification is detected, a command is executed.

  If --clear is present, we run /usr/bin/clear before the command
  execution.

  The command can be given in two ways: using the --script NAME, or
  given the command on the command line.

  Options:
  
    --script NAME or -s NAME
        executes ~/.test-continuous/NAME; exits if not found
    --clear
        executes /usr/bin/clear before the command execution
    --once
        executes the command once before starting to watch the directories

  My usual use case is:

    x-test-continuous -- prove -l -v t

  You need -- to split the arguments for this script and the
  arguments for the command to execute.

...

  print STDERR "FATAL: $mesg\n\n" if $mesg;
  
  exit($code || 2);
}

